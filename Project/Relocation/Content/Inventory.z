// AUTHOR: HUDSON GREEN
// CONTRIBUTORS: N/A

class Inventory : ZilchComponent {
    
    [Dependency] var Crafting : Crafting = null;
    
    var UISpace : Space = null;
    
    var InventoryOpen : Boolean = false;
    var InventoryOpenFirst : Boolean = false;
    var CraftingOpen : Boolean = false;
    var RightButtonDown : Boolean = false;
    
    var InventoryContents : Array[Integer];
    var InventoryContentsCount : Array[Integer];
    
    var MaxInventorySlots : Integer = 21;
    var SlotClicked : Integer = -1;
    var SlotHover : Integer = -1;
    
    var SLOT_HAND : Integer = 2;
    var SLOT_OFFHAND : Integer = 3;
    var SLOT_MOUSE : Integer = 1;
    
    var TooltipName : String = "";
    var TooltipLoreName : String = "";
    
    function Initialize(init : CogInitializer) {
        
        this.UISpace = this.LevelSettings.UICreator.UISpace;
        this.UISpace.LevelSettings.MainLevelReferances.Player = this.Owner;
        
        this.InventoryContents = new Array[Integer]();
        this.InventoryContentsCount = new Array[Integer]();
        
        // ZERO OUT INVENTORY
        for(var i = 0; i < this.MaxInventorySlots; ++i) {
            this.InventoryContents.Push(0);
            this.InventoryContentsCount.Push(0);
        }
        
        this.GiveItemInSlot(1, this.SLOT_HAND);
        
        Zero.Connect(this.UISpace, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(Zero.Keyboard, Events.KeyDown, this.OpenCloseInventory);
        
    }
    
    function OnLogicUpdate(event : UpdateEvent) {
        
        var inventory = this.UISpace.FindObjectByName("Inventory");
        
        // CHECK IF INVENTORY IS OPEN
        if(this.InventoryOpen) {
            
            this.Owner.Crafting.OnPausedLogicUpdate(event);
            this.RefreshInventoryIcons();
            this.MouseSlot();
            
            if(Zero.Keyboard.KeyIsPressed(Keys.C)) {
                this.CraftingOpen = !this.CraftingOpen;
            }
            
            if(!this.InventoryOpenFirst) {
                inventory.Transform.WorldTranslation = Real3(0, 0, 1);
                inventory.FindChildByName("CraftingMenu").Transform.WorldTranslation = Real3(0, 0, 50000);
                this.InventoryOpenFirst = true;
                return;
            } else {
                if(!this.CraftingOpen) {
                    inventory.FindChildByName("CraftingMenu").Transform.WorldTranslation = Real3(0, 0, 50000);
                    inventory.Transform.WorldTranslation = Real3(0, 0, 1);
                } else {
                    inventory.FindChildByName("CraftingMenu").Transform.WorldTranslation = Real3(10.75, 0, 0);
                    inventory.Transform.WorldTranslation = Real3(-4, 0, 1);
                }
            }
            
            this.Owner.Crafting.PausedLogicUpdateEnd();
            
        } else {
            
            this.InventoryOpenFirst = false;
            
            if(this.InventoryContents[this.SLOT_MOUSE - 1] != 0) {
                var tmpItem = this.InventoryContents[this.SLOT_MOUSE - 1];
                var tmpItemCt = this.InventoryContentsCount[this.SLOT_MOUSE - 1];
                this.InventoryContents[this.SLOT_MOUSE - 1] = 0;
                this.GiveItemWithAmount(tmpItem, false, tmpItemCt);
            }
            
            inventory.Transform.WorldTranslation = Real3(0, 0, 50000);
            
        }
        
    }
    
    function OpenCloseInventory(event : KeyboardEvent) {
        
        var commandScreenCapture = this.UISpace.FindObjectByName("DebugCommand").FindChildByName("TextEntry").TextEntry.CaptureKeys;
        var playerNameScreenCapture = this.UISpace.FindObjectByName("PlayerNameWrapper").FindChildByName("TextEntry").TextEntry.CaptureKeys;
        
        if(event.Key == Keys.E && !commandScreenCapture && !playerNameScreenCapture) {
            this.ToggleInventory();
        } else if(event.Key == Keys.Escape && !commandScreenCapture && !playerNameScreenCapture && this.InventoryOpen) {
            this.Space.TimeSpace.Paused = false;
            this.InventoryOpen = false;
        }
        
    }
    
    // TOGGLE INVENTORY AND TIMESPACE PAUSE
    function ToggleInventory() {
        this.Space.TimeSpace.Paused = !this.Space.TimeSpace.Paused;
        this.InventoryOpen = !this.InventoryOpen;
    }
    
    function RefreshInventoryIcons() {
        
        for(var i = 0; i < this.MaxInventorySlots; ++i) {
            
            if(this.InventoryContentsCount[i] == 0) {
                this.InventoryContents[i] = 0;
            } else if(this.InventoryContents[i] == 0 && this.InventoryContentsCount[i] > 0) {
                this.InventoryContentsCount[i] = 0;
            }
            
            var slotObj = this.UISpace.FindObjectByName("Slot`i + 1`");
            var itemMgmt = this.Space.FindObjectByName("LevelSettings").ItemManager;
            
            slotObj.FindChildByName("SlotIcon").Sprite.SpriteSource = Relocation.Items[this.InventoryContents[i]].Sprite;
            slotObj.FindChildByName("ItemCount").SpriteText.Text = "`this.InventoryContentsCount[i]`";
            slotObj.FindChildByName("ItemName").SpriteText.Text = Relocation.Items[this.InventoryContents[i]].ItemRecognizableName;
            
            if(slotObj.FindChildByName("SlotIcon").Sprite.SpriteSource == SpriteSource.Circle) {
                slotObj.FindChildByName("SlotIcon").Sprite.Visible = false;
                slotObj.FindChildByName("ItemCount").SpriteText.Visible = false;
                if(slotObj.Name == "Slot1") {
                    Relocation.UISpace.FindObjectByName("Tooltip").Tooltip.ShowTooltip(false);
                    
                }
            } else {
                slotObj.FindChildByName("SlotIcon").Sprite.Visible = true;
                slotObj.FindChildByName("ItemCount").SpriteText.Visible = true;
            }
            
            if(i == this.SLOT_HAND - 1 || i == this.SLOT_OFFHAND - 1) {
                
                slotObj = this.UISpace.FindObjectByName("Slot`i + 1`H");
                slotObj.FindChildByName("SlotIcon").Sprite.SpriteSource = Relocation.Items[this.InventoryContents[i]].Sprite;
                slotObj.FindChildByName("ItemCount").SpriteText.Text = "`this.InventoryContentsCount[i]`";
                
                if(slotObj.FindChildByName("SlotIcon").Sprite.SpriteSource == SpriteSource.Circle) {
                    slotObj.FindChildByName("SlotIcon").Sprite.Visible = false;
                    slotObj.FindChildByName("ItemCount").SpriteText.Visible = false;
                } else {
                    slotObj.FindChildByName("SlotIcon").Sprite.Visible = true;
                    slotObj.FindChildByName("ItemCount").SpriteText.Visible = true;
                }
                
            }
            
            this.Owner.Crafting.RefreshIcons(i);
            
        }
        
    }
    
    function MouseSlot() {
        
        // SET MOUSE SLOT POSITION
        var mousePos = this.UISpace.FindObjectByName("LevelSettings").CameraViewport.ScreenToWorldZPlane(Zero.Mouse.ScreenPosition, 5.0);
        this.UISpace.FindObjectByName("Slot1").Transform.WorldTranslation = mousePos;
        
        // MOVING ITEMS
        if(this.SlotClicked != -1 && this.SlotClicked != this.SLOT_MOUSE && Zero.Mouse.IsButtonDown(MouseButtons.Left)) {
            
            if(this.InventoryContents[this.SLOT_MOUSE - 1] == 0 && !Zero.Keyboard.KeyIsDown(Keys.Shift)) {
                this.MoveItem(this.SlotClicked, this.SLOT_MOUSE);
            } else if(!Zero.Keyboard.KeyIsDown(Keys.Shift)) {
                this.MoveItem(this.SLOT_MOUSE, this.SlotClicked);
            }
            
            if(this.InventoryContents[this.SLOT_MOUSE - 1] == 0 && Zero.Keyboard.KeyIsDown(Keys.Shift)) {
                this.MoveItem(this.SlotClicked, this.SLOT_HAND);
                this.MoveItem(this.SlotClicked, this.SLOT_OFFHAND);
            }
            
            this.SlotClicked = -1;
            
        } else if(this.SlotClicked != -1 && this.SlotClicked != this.SLOT_MOUSE && Zero.Mouse.IsButtonDown(MouseButtons.Right) && !Zero.Keyboard.KeyIsDown(Keys.Shift)) {
            
            this.RightButtonDown = true;
            
            var itemInSlot = this.InventoryContents[this.SlotClicked - 1];
            
            if(this.InventoryContents[this.SLOT_MOUSE - 1] == 0) {
                
                var itemCt = this.InventoryContentsCount[this.SlotClicked - 1];
                var itemCtFirstHalf = Math.Ceil(itemCt / 2.0) as Integer;
                var itemCtSecondHalf = itemCt - itemCtFirstHalf + itemCt % 2;
                
                this.RemoveItemsInSlot(this.SlotClicked, false, itemCtFirstHalf);
                this.GiveItemsInSlot(itemInSlot, this.SLOT_MOUSE, itemCtSecondHalf, true);
                
            } else if(this.InventoryContents[this.SLOT_MOUSE - 1] != 0 && (this.InventoryContents[this.SlotClicked - 1] == 0 || this.InventoryContents[this.SlotClicked - 1] == this.InventoryContents[this.SLOT_MOUSE - 1])) {
                this.GiveItemInSlot(this.InventoryContents[this.SLOT_MOUSE - 1], this.SlotClicked);
                this.RemoveItemInSlot(this.SLOT_MOUSE, false);
            }
            
            this.SlotClicked = -1;
            
        } else if(this.InventoryContents[this.SLOT_MOUSE - 1] != 0 && this.SlotClicked == -1 && Zero.Mouse.IsButtonDown(MouseButtons.Right) && !this.RightButtonDown) {
            this.RightButtonDown = true;
            this.RemoveItemInSlot(this.SLOT_MOUSE, true);
        } else if(this.InventoryContents[this.SLOT_MOUSE - 1] != 0 && this.SlotClicked == -1 && Zero.Mouse.IsButtonDown(MouseButtons.Right) && Zero.Keyboard.KeyIsDown(Keys.Shift)) {
            this.RightButtonDown = false;
            this.RemoveItemInSlot(this.SLOT_MOUSE, true);
        } else if(!Zero.Mouse.IsButtonDown(MouseButtons.Right) && !Zero.Keyboard.KeyIsDown(Keys.Shift)) {
            this.RightButtonDown = false;
        }
        
        // TOOLTIPS AND LORE
        if(this.InventoryContents[this.SLOT_MOUSE - 1] == 0 && this.SlotHover != -1 && this.SlotHover != this.SLOT_MOUSE) {
            
            if(this.Owner.Crafting.GetTooltipName() == "State.False") {
                this.TooltipName = Relocation.Items[this.InventoryContents[this.SlotHover - 1]].ItemRecognizableName;
                this.TooltipLoreName = Relocation.Items[this.InventoryContents[this.SlotHover - 1]].ItemLore;
            }
            
            if(this.TooltipName != "") {
                
                var itemMgmt = this.Space.FindObjectByName("LevelSettings").ItemManager;
                var tooltipColor = Real4(1.0);
                var tooltipLoreColor = Real4(1.0, 1.0, 1.0, 0.9);
                
                if(Relocation.Items[this.InventoryContents[this.SlotHover - 1]].ItemType == "WeaponCommon") {
                    tooltipColor = Real4(0.1, 1.0, 0.1, 1.0);
                } else if(Relocation.Items[this.InventoryContents[this.SlotHover - 1]].ItemType == "WeaponEpic") {
                    tooltipColor = Real4(0.7, 0.3, 0.9, 1.0);
                } else if(Relocation.Items[this.InventoryContents[this.SlotHover - 1]].ItemType == "WeaponLegendary") {
                    tooltipColor = Real4(1.0, 0.7, 0.0, 1.0);
                } else if(Relocation.Items[this.InventoryContents[this.SlotHover - 1]].ItemType == "Resource") {
                    tooltipColor = Real4(0.6, 0.6, 0.6, 1.0);
                } else if(Relocation.Items[this.InventoryContents[this.SlotHover - 1]].ItemType == "Health") {
                    tooltipColor = Real4(1.0, 0.0, 0.0, 1.0);
                }
                
                Relocation.UISpace.FindObjectByName("Tooltip").Tooltip.ShowTooltip(true);
                Relocation.UISpace.FindObjectByName("Tooltip").Tooltip.TooltipText(this.TooltipName, this.TooltipLoreName, tooltipColor, tooltipLoreColor);
                
            }
            
            this.SlotHover = -1;
            
        } else if(this.Owner.Crafting.GetTooltipName() == "State.False") {
            
            this.TooltipName = "";
            this.TooltipLoreName = "";
            
            Relocation.UISpace.FindObjectByName("Tooltip").Tooltip.ShowTooltip(false);
            
            this.SlotHover = -1;
            
        } else {
            
            this.TooltipName = this.Owner.Crafting.GetTooltipName();
            this.TooltipLoreName = "";
            
            Relocation.UISpace.FindObjectByName("Tooltip").Tooltip.ShowTooltip(true);
            Relocation.UISpace.FindObjectByName("Tooltip").Tooltip.TooltipText(this.TooltipName, this.TooltipLoreName, Real4(1.0), Real4(1.0));
            
            this.SlotHover = -1;
            
        }
        
    }
    
    function GiveItem(itemID : Integer, notifyUser : Boolean) {
        
        var firstAvailableSlot = -1;
        var cmd : TextEntry = null;
        
        itemID = Math.Abs(itemID);
        
        if(notifyUser) {
            cmd = this.UISpace.FindObjectByName("DebugCommand").FindChildByName("TextEntry").TextEntry;
        }
        
        if(itemID < Relocation.Items.Count) {
            
            var hasGoneThru = false;
            
            for(var i = this.SLOT_OFFHAND; i < this.MaxInventorySlots + this.SLOT_OFFHAND; ++i) {
                if((i < this.MaxInventorySlots && !hasGoneThru) || (i < this.SLOT_OFFHAND && hasGoneThru)) {
                    if(this.InventoryContents[i] == 0 && firstAvailableSlot == -1) {
                        firstAvailableSlot = i;
                    } else if(this.InventoryContents[i] == itemID) {
                        ++this.InventoryContentsCount[i];
                        this.RefreshInventoryIcons();
                        if(notifyUser)
                            cmd.PrintToUIConsole("Item with the ID \"`itemID`\" has been added to your inventory.");
                        return;
                    }
                } else if(i >= this.SLOT_OFFHAND && hasGoneThru) {
                    break;
                } else {
                    i = 0;
                    hasGoneThru = true;
                }
            }
            
            if(firstAvailableSlot != -1) {
                this.InventoryContents[firstAvailableSlot] = itemID;
                this.InventoryContentsCount[firstAvailableSlot] = 1;
                this.RefreshInventoryIcons();
                if(notifyUser)
                    cmd.PrintToUIConsole("Item with the ID \"`itemID`\" has been added to your inventory.");
                return;
            }
            
        } else {
            this.UISpace.FindObjectByName("DebugCommand").FindChildByName("TextEntry").TextEntry.PrintToUIConsole("Error: An item with the ID \"`itemID`\" does not exist.");
            return;
        }
        
        Console.WriteLine("Warning: Inventory full. Item with the ID \"`itemID`\" was not added to the inventory.");
        
    }
    
    function GiveItemWithAmount(itemID : Integer, notifyUser : Boolean, amount : Integer) {
        
        var firstAvailableSlot = -1;
        var cmd : TextEntry = null;
        
        itemID = Math.Abs(itemID);
        
        if(notifyUser) {
            cmd = this.UISpace.FindObjectByName("DebugCommand").FindChildByName("TextEntry").TextEntry;
        }
        
        if(itemID < Relocation.Items.Count) {
            
            var hasGoneThru = false;
            
            for(var i = this.SLOT_OFFHAND; i < this.MaxInventorySlots + this.SLOT_OFFHAND; ++i) {
                if((i < this.MaxInventorySlots && !hasGoneThru) || (i < this.SLOT_OFFHAND && hasGoneThru)) {
                    if(this.InventoryContents[i] == 0 && firstAvailableSlot == -1) {
                        firstAvailableSlot = i;
                    } else if(this.InventoryContents[i] == itemID) {
                        this.InventoryContentsCount[i] += amount;
                        this.RefreshInventoryIcons();
                        if(notifyUser)
                            cmd.PrintToUIConsole("Item with the ID \"`itemID`\" has been added to your inventory.");
                        return;
                    }
                } else if(i >= this.SLOT_OFFHAND && hasGoneThru) {
                    break;
                } else {
                    i = 0;
                    hasGoneThru = true;
                }
            }
            
            if(firstAvailableSlot != -1) {
                this.InventoryContents[firstAvailableSlot] = itemID;
                this.InventoryContentsCount[firstAvailableSlot] = amount;
                this.RefreshInventoryIcons();
                if(notifyUser)
                    cmd.PrintToUIConsole("Item with the ID \"`itemID`\" has been added to your inventory.");
                return;
            }
            
        } else {
            this.UISpace.FindObjectByName("DebugCommand").FindChildByName("TextEntry").TextEntry.PrintToUIConsole("Error: An item with the ID \"`itemID`\" does not exist.");
            return;
        }
        
        Console.WriteLine("Warning: Inventory full. Item with the ID \"`itemID`\" was not added to the inventory.");
        
    }
    
    function GiveItemInSlot(itemID : Integer, slot : Integer) {
        
        itemID = Math.Abs(itemID);
        
        if(itemID < Relocation.Items.Count) {
            if(slot < this.MaxInventorySlots && slot > this.SLOT_MOUSE) {
                
                if(this.InventoryContents[slot - 1] == 0) {
                    this.InventoryContents[slot - 1] = itemID;
                    this.InventoryContentsCount[slot - 1] = 1;
                    this.RefreshInventoryIcons();
                    return;
                } else if(this.InventoryContents[slot - 1] == itemID) {
                    ++this.InventoryContentsCount[slot - 1];
                    this.RefreshInventoryIcons();
                    return;
                }
                
            }
        } else {
            this.UISpace.FindObjectByName("DebugCommand").FindChildByName("TextEntry").TextEntry.PrintToUIConsole("Error: An item with the ID \"`itemID`\" does not exist.");
            return;
        }
        
    }
    
    function GiveItemsInSlot(itemID : Integer, slot : Integer, amount : Integer, allowInMouseSlot : Boolean) {
        
        itemID = Math.Abs(itemID);
        
        if(itemID < Relocation.Items.Count) {
            if(slot < this.MaxInventorySlots && slot > this.SLOT_MOUSE) {
                
                if(this.InventoryContents[slot - 1] == 0) {
                    this.InventoryContents[slot - 1] = itemID;
                    this.InventoryContentsCount[slot - 1] = amount;
                    this.RefreshInventoryIcons();
                    return;
                } else if(this.InventoryContents[slot - 1] == itemID) {
                    this.InventoryContentsCount[slot - 1] += amount;
                    this.RefreshInventoryIcons();
                    return;
                }
                
            } else if(slot == this.SLOT_MOUSE && allowInMouseSlot) {
                
                if(this.InventoryContents[slot - 1] == 0) {
                    this.InventoryContents[slot - 1] = itemID;
                    this.InventoryContentsCount[slot - 1] = amount;
                    this.RefreshInventoryIcons();
                    return;
                } else if(this.InventoryContents[slot - 1] == itemID) {
                    this.InventoryContentsCount[slot - 1] += amount;
                    this.RefreshInventoryIcons();
                    return;
                }
                
            }
        } else {
            this.UISpace.FindObjectByName("DebugCommand").FindChildByName("TextEntry").TextEntry.PrintToUIConsole("Error: An item with the ID \"`itemID`\" does not exist.");
            return;
        }
        
    }
    
    function CheckForItem(itemID : Integer) : Boolean {
        
        /*itemID = Math.Abs(itemID);
        
        for(var i = 0; i < this.MaxInventorySlots; ++i) {
            if(this.InventoryContents[i] == itemID) {
                return true;
            }
        }
        
        return false;*/
        
        return this.CheckForItems(itemID, 1);
        
    }
    
    function CheckForItems(itemID : Integer, amount : Integer) : Boolean {
        
        itemID = Math.Abs(itemID);
        
        var itemCount = 0;
        
        for(var i = 0; i < this.MaxInventorySlots; ++i) {
            if(this.InventoryContents[i] == itemID) {
                itemCount += this.InventoryContentsCount[i];
            }
        }
        
        if(itemCount >= amount) {
            return true;
        } else {
            return false;
        }
        
    }
    
    function CheckForItemMainHand(itemID : Integer) : Boolean {
        
        itemID = Math.Abs(itemID);
            
        if(this.InventoryContents[this.SLOT_HAND - 1] == itemID) {
            return true;
        } else {
            return false;
        }
        
    }
    
    function GetItemInMainHand() : Integer {
        return this.InventoryContents[this.SLOT_HAND - 1];
    }
    
    function MoveItem(slot : Integer, destSlot : Integer) {
        
        if(slot - 1 < this.MaxInventorySlots && destSlot - 1 < this.MaxInventorySlots) {
            
            var tmpItem = this.InventoryContents[slot - 1];
            var tmpItemCt = this.InventoryContentsCount[slot - 1];
            
            if(this.InventoryContents[slot - 1] == 0) {
                return;
            } else if(this.InventoryContents[destSlot - 1] != 0 && this.InventoryContents[destSlot - 1] != tmpItem) {
                this.InventoryContents[slot - 1] = this.InventoryContents[destSlot - 1];
                this.InventoryContentsCount[slot - 1] = this.InventoryContentsCount[destSlot - 1];
                this.InventoryContents[destSlot - 1] = tmpItem;
                this.InventoryContentsCount[destSlot - 1] = tmpItemCt;
                return;
            } else {
                
                this.InventoryContents[slot - 1] = 0;
                this.InventoryContentsCount[slot - 1] = 0;
                
                if(this.InventoryContents[destSlot - 1] == tmpItem) {
                    this.InventoryContentsCount[destSlot - 1] += tmpItemCt;
                } else {
                    this.InventoryContents[destSlot - 1] = tmpItem;
                    this.InventoryContentsCount[destSlot - 1] = tmpItemCt;
                }
                
            }
            
            this.RefreshInventoryIcons();
            
        }
        
    }
    
    function RemoveItem(itemID : Integer) {
        this.RemoveItems(itemID, 1);
    }
    
    function RemoveItems(itemID : Integer, amount : Integer) {
        
        itemID = Math.Abs(itemID);
        
        if(itemID < Relocation.Items.Count) {
            for(var i = 0; i < this.MaxInventorySlots; ++i) {
                if(this.InventoryContents[i] == itemID) {
                    this.InventoryContentsCount[i] -= amount;
                    this.RefreshInventoryIcons();
                    return;
                }
            }
        } else {
            this.UISpace.FindObjectByName("DebugCommand").FindChildByName("TextEntry").TextEntry.PrintToUIConsole("Error: An item with the ID \"`itemID`\" does not exist.");
            return;
        }
        
    }
    
    function RemoveItemInSlot(slot : Integer, drop : Boolean) {
        this.RemoveItemsInSlot(slot, drop, 1);
    }
    
    function RemoveItemsInSlot(slot : Integer, drop : Boolean, amount : Integer) {
        
        var itemID = -1;
        
        if(this.InventoryContents[slot - 1] != 0 && this.InventoryContentsCount[slot - 1] > 0) {
            itemID = this.InventoryContents[slot - 1];
            this.InventoryContentsCount[slot - 1] -= amount;
            this.RefreshInventoryIcons();
        }
        
        if(drop && itemID != -1) {
            Console.WriteLine("Implement drops. In the meantime, the item that was dropped will be deleted.");
        }
        
    }
    
}
