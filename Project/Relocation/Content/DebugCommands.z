// AUTHOR: HUDSON GREEN
// CONTRIBUTORS: N/A

class TextEntry : ZilchComponent {
    
    [Property] var IsCommandTextBox : Boolean = false;
    [Property] var ConsoleTextboxName : String = "Console";
    [Property] var TextBoxBackground : String = "TextBoxBG";
    [Property] var TextCharDimensions : Real2 = Real2(0.26, 0.5);
    [Property] var MaxCharsPerLine : Integer = 75;
    [Property] var MaxCharLimit : Integer = 50000;
    
    var DebugEnabled : Boolean = false;
    var GodMode : Boolean = false;
    
    var CaptureKeys : Boolean = false;
    var CycleThroughCmdsAll : Boolean = false;
    var IsTextSelected : Boolean = false;
    var WasClicked : Boolean = false;
    var CursorMoveClick : Boolean = false;
    var BlinkTypingIndicator : Boolean = true;
    
    var CommandString : String = "";
    var FinalCmd : String = "";
    var ConsoleLog : String = "";
    var TempCommandStr : String = "";
    var TypingIndicator : String = "|";
    var CurrTypingIndicator : String = "|";
    
    var CommandHistory : Array[String] = null;
    var Commands : Array[String] = null;
    
    var CmdHistoryIndex : Integer = 0;
    var AutocompleteIndex : Integer = 0;
    var CursorIndex : Integer = 0;
    var OverflowOffset : Integer = 0;
    
    var Occurances : Integer = 1;
    
    var TypingIndicatorBlinkTime : Real = 1.0;
    var CurrTypingIndicatorBlinkTime : Real = 0.0;
    var RepeatDelay : Real = 0.5;
    var CurrRepeatDelay : Real = 0.0;
    var HeldKeyRepeatSpeed : Real = 0.005;
    var CurrHeldKeyRepeatSpeed : Real = 0.0;
    
    var SelectedTextIndex : Integer2 = Integer2(0, 0);
    
    function Initialize(init : CogInitializer) {
        
        this.TextCharDimensions = Real2(this.Owner.SpriteText.MeasureGivenText("G").X, this.TextCharDimensions.Y);
        
        this.CommandHistory = new Array[String]();
        
        if(this.Owner.Commands != null)
            this.Owner.Commands.CreateCommands();
        
        Zero.Connect(this.GameSession, Events.GameRequestQuit, this.OnGameRequestQuit);
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.MouseUpdate, this.OnMouseUpdate);
        Zero.Connect(Zero.Keyboard, Events.TextTyped, this.OnTextTyped);
        
    }
    
    function OnGameRequestQuit(event : GameEvent) {
        event.Handled = true;
    }
    
    function OnLogicUpdate(event : UpdateEvent) {
        
        if(this.CaptureKeys) {
            
            // CURSOR BLINKING
            if(this.CurrTypingIndicatorBlinkTime > this.TypingIndicatorBlinkTime) {
                this.CurrTypingIndicatorBlinkTime = 0.0;
            } else {
                
                if(this.CurrTypingIndicatorBlinkTime < this.TypingIndicatorBlinkTime / 2 && this.BlinkTypingIndicator) {
                    this.CurrTypingIndicator = this.TypingIndicator;
                } else if(this.BlinkTypingIndicator || this.IsTextSelected) {
                    this.CurrTypingIndicator = " ";
                } else if(!this.BlinkTypingIndicator) {
                    this.CurrTypingIndicatorBlinkTime = 0.0;
                    this.CurrTypingIndicator = this.TypingIndicator;
                }
                
                this.CurrTypingIndicatorBlinkTime += event.Dt;
                
            }
            
            // CONTROLS
            this.Controls(event);
            
            if(!this.IsTextSelected) {
                
                var txtSelect = this.Owner.Parent.FindChildByName("TextBoxSelection");
                
                txtSelect.Sprite.Visible = false;
                txtSelect.Transform.Scale = Real3(this.TextCharDimensions.X, this.TextCharDimensions.Y, 1);
                
                this.SelectedTextIndex = Integer2(this.CursorIndex);
                
                this.BlinkTypingIndicator = true;
                
            }
            
            if(this.CommandString.Count <= this.MaxCharsPerLine) {
                this.Owner.SpriteText.Text = this.CommandString;
                this.OverflowOffset = 0;
            } else {
                this.Owner.SpriteText.Text = 
                    this.CommandString.SubStringFromRuneIndices(
                        this.CommandString.Count - (this.MaxCharsPerLine), this.CommandString.Count
                    );
                this.OverflowOffset = this.CommandString.Count - this.Owner.SpriteText.Text.Count;
            }
            
            this.UpdateCursorPosition(true);
            
        } else if(this.Owner.SpriteText.Text.Count > 0) {
            this.Owner.SpriteText.Text = "";
        }
        
        // SHOW/HIDE THINGS WHEN TEXT BOX IS TOGGLED
        if(this.CaptureKeys) {
            this.Owner.Parent.FindChildByName(this.TextBoxBackground).Sprite.Visible = true;
            this.Owner.Parent.FindChildByName("TextCursor").SpriteText.Visible = true;
            if(this.Owner.Parent.SpriteText != null)
                this.Owner.Parent.SpriteText.Visible = true;
            if(this.IsCommandTextBox)
                this.Owner.Parent.FindChildByName(this.ConsoleTextboxName).SpriteText.Visible = true;
        } else {
            this.Owner.Parent.FindChildByName(this.TextBoxBackground).Sprite.Visible = false;
            this.Owner.Parent.FindChildByName("TextCursor").SpriteText.Visible = false;
            if(this.Owner.Parent.SpriteText != null)
                this.Owner.Parent.SpriteText.Visible = false;
            if(this.IsCommandTextBox)
                this.Owner.Parent.FindChildByName(this.ConsoleTextboxName).SpriteText.Visible = false;
            this.IsTextSelected = false;
        }
        
        if(((Zero.Keyboard.KeyIsPressed(Keys.Tilde) && this.IsCommandTextBox) || (this.WasClicked && !this.IsCommandTextBox)) && !this.CaptureKeys) {
            this.CaptureKeys = true;
            this.WasClicked = false;
        }
        
        // COMMANDS
        if(this.FinalCmd.Count > 0 && this.IsCommandTextBox)
            this.Owner.Commands.CommandFunctions();
        
    }
    
    function OnMouseUpdate(event : ViewportMouseEvent) {
        
        if(!this.WasClicked && event.Mouse.IsButtonDown(MouseButtons.Left))
            this.WasClicked = true;
        
        if(event.Mouse.IsButtonDown(MouseButtons.Left) && !this.CursorMoveClick) {
            this.CursorIndex = this.CalcTextIndexFromPosition(event.CameraViewport.ScreenToWorldZPlane(event.Position, 0.0).X);
            this.SelectedTextIndex = Integer2(this.CursorIndex);
            this.CursorMoveClick = true;
            this.CatchInvalidCursorPos();
        } else if(!event.Mouse.IsButtonDown(MouseButtons.Left) && this.CursorMoveClick) {
            this.CursorMoveClick = false;
        } else if(event.Mouse.IsButtonDown(MouseButtons.Left)) {
            this.SelectedTextIndex = Integer2(this.SelectedTextIndex.X, this.CalcTextIndexFromPosition(event.CameraViewport.ScreenToWorldZPlane(event.Position, 0.0).X));
        }
        
        this.SelectText(this.SelectedTextIndex, false);
        
        Console.WriteLine("Selected Index: `this.SelectedTextIndex`");
        
    }
    
    function OnTextTyped(event : KeyboardTextEvent) {
        
        if(this.CaptureKeys) {
            
            this.CatchInvalidCursorPos();
            
            // CHECK IF TEXT IS RENDERABLE (IS A LETTER/NUMBER/SYMBOL)
            if(this.IsRenderable(event.Character) && this.CommandString.Count < this.MaxCharLimit) {
                
                this.CurrTypingIndicatorBlinkTime = 0.0;
                this.CycleThroughCmdsAll = false;
                
                if(!this.IsTextSelected) {
                    this.CommandString = this.InsertAt(String.FromChar(event.Character));
                    ++this.CursorIndex;
                } else {
                    this.IsTextSelected = false;
                    this.CommandString = this.ReplaceInRange(this.SelectText(this.SelectedTextIndex, true), "`String.FromChar(event.Character)`", true);
                    this.CursorIndex += 1;
                    this.CatchInvalidCursorPos();
                }
                
                this.TempCommandStr = this.CommandString;
                
            }
            
            // CHECK FOR BACKSPACE
            if(event.Character == 8 && this.CommandString.Count > 0) {
                
                this.CurrTypingIndicatorBlinkTime = 0.0;
                this.CycleThroughCmdsAll = false;
                
                if(!this.IsTextSelected && this.CursorIndex > 0) {
                    this.CommandString = this.RemoveAt(0);
                    --this.CursorIndex;
                } else if(this.IsTextSelected) {
                    this.IsTextSelected = false;
                    this.CommandString = this.ReplaceInRange(this.SelectText(this.SelectedTextIndex, true), "", true);
                }
                
                this.TempCommandStr = this.CommandString;
                
            }
            
            // CHECK FOR TAB
            if(event.Character == 9 && this.IsCommandTextBox) {
                
                this.IsTextSelected = false;
                
                if(this.AutocompleteIndex >= this.Commands.Count)
                    this.AutocompleteIndex = 0;
                
                if(this.CommandString == "" || this.CycleThroughCmdsAll) {
                    
                    this.CurrTypingIndicatorBlinkTime = 0.0;
                    this.CycleThroughCmdsAll = true;
                    
                    this.CommandString = this.Commands.Get(this.AutocompleteIndex);
                    this.CursorIndex = this.CommandString.Count;
                    
                } else {
                    
                    var cmdStr = this.Commands.Get(this.SearchArray(this.Commands, this.TempCommandStr, this.AutocompleteIndex));
                    
                    if(this.Occurances != 0) {
                        this.CurrTypingIndicatorBlinkTime = 0.0;
                        this.CommandString = cmdStr;
                        this.CursorIndex = this.CommandString.Count;
                    }
                    
                    this.CycleThroughCmdsAll = false;
                    this.Occurances = 1;
                    
                }
                
                ++this.AutocompleteIndex;
                
            }
            
        }
        
    }
    
    function Controls(event : UpdateEvent) {
        
        if(Zero.Keyboard.KeyIsPressed(Keys.Left)) {
            this.MoveCursor(-1, -1);
            this.CurrHeldKeyRepeatSpeed = 0.0;
            this.CurrRepeatDelay = 0.0;
            return;
        } else if(Zero.Keyboard.KeyIsPressed(Keys.Right)) {
            this.MoveCursor(-1, 1);
            this.CurrHeldKeyRepeatSpeed = 0.0;
            this.CurrRepeatDelay = 0.0;
            return;
        }
        
        if(Zero.Keyboard.KeyIsReleased(Keys.Escape)) {
            
            this.CommandString = "";
            this.TempCommandStr = "";
            
            this.CaptureKeys = false;
            this.IsTextSelected = false;
            this.CycleThroughCmdsAll = false;
            this.WasClicked = false;
            
            this.CmdHistoryIndex = this.CommandHistory.Count;
            this.AutocompleteIndex = 0;
            this.CurrTypingIndicatorBlinkTime = 0.0;
            
        } else if(Zero.Keyboard.KeyIsPressed(Keys.Enter)) {
            
            this.FinalCmd = this.CommandString;
            
            this.IsTextSelected = false;
            this.CycleThroughCmdsAll = false;
            this.WasClicked = false;
            
            if(this.CommandString != "") {
                
                this.CmdHistoryIndex = this.CommandHistory.Count + 1;
                this.CommandHistory.Push(this.CommandString);
                
                if(!this.IsCommandTextBox) {
                    this.CaptureKeys = false;
                    this.Owner.Parent.FindChildByName(this.ConsoleTextboxName).SpriteText.Text = this.FinalCmd;
                }
                
            }
            
            this.CommandString = "";
            this.TempCommandStr = "";
            
        } else if(Zero.Keyboard.KeyIsPressed(Keys.Up) && this.IsCommandTextBox) {
            
            this.IsTextSelected = false;
            
            if(this.CommandHistory.Count > 0 && this.CmdHistoryIndex > 0) {
                
                --this.CmdHistoryIndex;
                
                this.CommandString = this.CommandHistory.Get(this.CmdHistoryIndex);
                this.TempCommandStr = this.CommandString;
                this.CursorIndex = this.CommandString.Count;
                
            }
            
        } else if(Zero.Keyboard.KeyIsPressed(Keys.Down) && this.IsCommandTextBox) {
            
            this.IsTextSelected = false;
            
            if(this.CommandHistory.Count > 0 && this.CmdHistoryIndex < this.CommandHistory.Count - 1) {
                
                ++this.CmdHistoryIndex;
                
                this.CommandString = this.CommandHistory.Get(this.CmdHistoryIndex);
                this.TempCommandStr = this.CommandString;
                this.CursorIndex = this.CommandString.Count;
                
            } else if(this.CmdHistoryIndex < this.CommandHistory.Count) {
                
                ++this.CmdHistoryIndex;
                
                this.CommandString = "";
                this.TempCommandStr = "";
                
            }
            
        } else if(Zero.Keyboard.KeyIsDown(Keys.Control) && Zero.Keyboard.KeyIsPressed(Keys.A)) {
            this.SelectedTextIndex = Integer2(0, this.CommandString.Count);
            this.SelectText(this.SelectedTextIndex, false);
        } else if(Zero.Keyboard.KeyIsDown(Keys.Control) && Zero.Keyboard.KeyIsPressed(Keys.C) && this.IsTextSelected) {
            var selection = this.SelectText(this.SelectedTextIndex, true);
            Zero.Shell.ClipboardText = this.CommandString.SubStringFromRuneIndices(selection.X, selection.Y);
        } else if(Zero.Keyboard.KeyIsDown(Keys.Control) && Zero.Keyboard.KeyIsPressed(Keys.X) && this.IsTextSelected) {
            var selection = this.SelectText(this.SelectedTextIndex, true);
            Zero.Shell.ClipboardText = this.CommandString.SubStringFromRuneIndices(selection.X, selection.Y);
            this.CommandString = this.ReplaceInRange(this.SelectText(this.SelectedTextIndex, true), "", true);
            this.TempCommandStr = this.CommandString;
            this.IsTextSelected = false;
        } else if(Zero.Keyboard.KeyIsDown(Keys.Control) && Zero.Keyboard.KeyIsPressed(Keys.V)) {
            this.PasteText();
        } else if(Zero.Keyboard.KeyIsDown(Keys.Left)) {
            this.MoveCursor(event.Dt, -1);
            return;
        } else if(Zero.Keyboard.KeyIsDown(Keys.Right)) {
            this.MoveCursor(event.Dt, 1);
            return;
        } else {
            this.CurrRepeatDelay = 0.0;
            this.CurrHeldKeyRepeatSpeed = 0.0;
        }
        
    }
    
    // FOR ARROW KEYS
    function MoveCursor(dt : Real, amount : Integer) {
        
        if(!Zero.Keyboard.KeyIsDown(Keys.Shift))
            this.IsTextSelected = false;
        
        if(this.CurrRepeatDelay > this.RepeatDelay && this.CurrHeldKeyRepeatSpeed > this.HeldKeyRepeatSpeed || dt < 0) {
            this.CursorIndex += amount;
            this.CurrHeldKeyRepeatSpeed = 0.0;
        } else {
            this.CurrRepeatDelay += dt;
            this.CurrHeldKeyRepeatSpeed += dt;
        }
        
        this.CurrTypingIndicatorBlinkTime = 0.0;
        this.CatchInvalidCursorPos();
        
    }
    
    function UpdateCursorPosition(updatePos : Boolean) {
        
        this.CatchInvalidCursorPos();
        
        var txtCursor = this.Owner.Parent.FindChildByName("TextCursor");
        var cmdStrFront = this.CommandString.SubStringFromRuneIndices(0, this.CursorIndex);
        
        txtCursor.SpriteText.Text = this.CurrTypingIndicator;
        
        if(updatePos)
            txtCursor.Transform.WorldTranslation = Real3(
                this.Owner.Transform.WorldTranslation.X + this.Owner.SpriteText.MeasureGivenText(cmdStrFront).X - (this.TextCharDimensions.X * 0.5) - 
                    this.OverflowOffset * this.TextCharDimensions.X,
                txtCursor.Transform.WorldTranslation.YZ
            );
        
    }
    
    function CalcTextIndexFromPosition(posX : Real) : Integer {
        
        var startPos = this.Owner.Transform.WorldTranslation.X;
        var charWidth = this.TextCharDimensions.X;
        
        var charPos = Math.Floor((posX - startPos) / charWidth + 0.25) as Integer;
        
        return charPos;
        
    }
    
    function CalcTextPositionFromIndex(index : Integer) : Real {
        
        var startPos = this.Owner.Transform.WorldTranslation.X;
        var charWidth = this.TextCharDimensions.X;
        
        var charPos = this.Owner.Transform.WorldTranslation.X + index * charWidth;
        
        return charPos;
        
    }
    
    function SelectText(selectedIndex : Integer2, returnOrderedSelection : Boolean) : Integer2 {
        
        if(selectedIndex.X > selectedIndex.Y)
            selectedIndex = selectedIndex.YX;
        
        if(selectedIndex.X < 0)
            selectedIndex.X = 0;
        if(selectedIndex.Y > this.CommandString.Count)
            selectedIndex.Y = this.CommandString.Count;
        
        if(returnOrderedSelection) {
            return selectedIndex;
        }
        
        if(selectedIndex.X == selectedIndex.Y) {
            this.IsTextSelected = false;
            return selectedIndex;
        }
        
        var selectDiff = selectedIndex.Y - selectedIndex.X;
        
        this.IsTextSelected = true;
        this.BlinkTypingIndicator = false;
        this.CursorIndex = selectedIndex.Y;
        
        var txtSelect = this.Owner.Parent.FindChildByName("TextBoxSelection");
        
        txtSelect.Sprite.Visible = true;
        txtSelect.Transform.WorldTranslation = Real3(this.CalcTextPositionFromIndex(selectedIndex.X), txtSelect.Transform.WorldTranslation.YZ);
        txtSelect.Transform.Scale = Real3((selectDiff) * this.TextCharDimensions.X, this.TextCharDimensions.Y, 1);
        
        return selectedIndex;
        
    }
    
    function PasteText() {
        
        var tmpClipboard = "";
        
        // STRIP INVALID CHARACTERS
        for(var i = 0; i < Zero.Shell.ClipboardText.Count; ++i) {
            if(this.IsRenderable(Zero.Shell.ClipboardText[i]))
                tmpClipboard = "`tmpClipboard``String.FromChar(Zero.Shell.ClipboardText[i])`";
        }
        
        if(!this.IsTextSelected) {
            this.CommandString = this.InsertAt(tmpClipboard);
        } else {
            this.IsTextSelected = false;
            this.CommandString = this.ReplaceInRange(this.SelectText(this.SelectedTextIndex, true), "`tmpClipboard`", true);
        }
        
        this.CursorIndex += tmpClipboard.Count;
        
        this.CurrTypingIndicatorBlinkTime = 0.0;
        this.CycleThroughCmdsAll = false;
        
        this.TempCommandStr = this.CommandString;
        this.CatchInvalidCursorPos();
        
    }
    
    function ReplaceInRange(range : Integer2, newTxt : String, moveCursor : Boolean) : String {
        var start = this.CommandString.SubStringFromRuneIndices(0, range.X);
        var end = this.CommandString.SubStringFromRuneIndices(range.Y, this.CommandString.Count);
        if(moveCursor)
            this.CursorIndex = start.Count;
        this.CatchInvalidCursorPos();
        return "`start``newTxt``end`";
    }
    
    function PrintToUIConsole(text : String) {
        this.PrintToUIConsole(text, false, "");
    }
    
    function PrintToUIConsole(beginningText : String, endingText : String, beginningFixedTextWidth : Integer) {
        
        var spaceCount = beginningFixedTextWidth - beginningText.Count;
        
        for(var i = 0; i < spaceCount; ++i)
            beginningText = "`beginningText` ";
        
        this.PrintToUIConsole("`beginningText``endingText`", false, "");
        
    }
    
    function PrintToUIConsole(text : String, header : Boolean) {
        this.PrintToUIConsole(" `text` ", header, "_");
    }
    
    function PrintToUIConsole(text : String, centerText : Boolean, centerChar : String) {
        
        var maxLength = this.MaxCharsPerLine;
        var tmpTxt = "";
        
        if(centerText) {
            
            var centerTmpTxt = "";
            
            for(var i = 0; i < Math.Floor((maxLength - text.Count) / 2) as Integer; ++i)
                centerTmpTxt = "`centerTmpTxt``centerChar`";
            
            text = "`centerTmpTxt``text``centerTmpTxt`";
            
        }
        
        if(text.Count > maxLength) {
            for(var i = (Math.Floor(text.Count / maxLength) * maxLength) as Integer; i > 5; i -= maxLength) {
                
                var breakIndex = i;
                
                for(var j = i; j > i - maxLength / 4; --j) {
                    
                    if(String.FromChar(text.Get(j)) == " ") {
                        breakIndex = j + 1;
                        break;
                    }
                    
                    if(j + 1 == i - maxLength / 4) {
                        breakIndex = i - 8;
                        break;
                    }
                    
                }
                
                var tmpFrnt = text.SubStringFromRuneIndices(0, breakIndex);
                tmpTxt = text.SubStringFromRuneIndices(breakIndex, text.Count);
                tmpFrnt = "`tmpFrnt`\n";
                text = "`tmpFrnt``tmpTxt`";
                
            }
        }
        
        text = String.Concatenate("\n", text);
        this.ConsoleLog = String.Concatenate(this.ConsoleLog, text);
        
        var console = this.Space.FindObjectByName(this.ConsoleTextboxName);
        console.SpriteText.Text = this.ConsoleLog;
        console.Transform.Translation = Real3(0, console.SpriteText.MeasureText().Y + 1.75, console.Transform.Translation.Z);
        
    }
    
    function SearchArray(array : Array[String], str : String, curIndex : Integer) : Integer {
        
        var occurances = 0;
        var startIndex = 0;
        var tmpOccur = 0;
        var tmpIndex = 0;
        
        // FIND NUMBER OF OCCURANCES
        for(var i = 0; i < array.Count; ++i) {
            if(array.Get(i).StartsWith(str) && occurances == 0)
                startIndex = i;
            if(array.Get(i).StartsWith(str))
                ++occurances;
        }
        
        if(this.AutocompleteIndex < startIndex) {
            this.AutocompleteIndex = startIndex;
            return startIndex;
        }
        
        this.Occurances = occurances;
        
        for(var i = 0; i < occurances; ++i) {
            tmpIndex = startIndex + tmpOccur;
            if(array.Get(tmpIndex).StartsWith(str) && curIndex <= tmpIndex && tmpOccur < occurances && tmpIndex < array.Count)
                return tmpIndex;
            else
                ++tmpOccur;
        }
        
        if(tmpOccur >= occurances || tmpOccur == 0) {
            this.AutocompleteIndex = startIndex;
            return startIndex;
        }
        
        return -1;
        
    }
    
    function GetArgument(argNum : Integer, command : String) : String {
        
        var currArg = 0;
        var tmpArg = "";
        
        for(var i = 0; i < command.Count; ++i) {
            
            if(String.FromChar(command.Get(i)) == " ") {
                ++currArg;
                continue;
            }
            
            if(currArg == argNum)
                tmpArg = String.Concatenate(tmpArg, String.FromChar(command.Get(i)));
            else if(currArg > argNum)
                break;
            
        }
        
        return tmpArg;
        
    }
    
    function IsRenderable(code : Integer) : Boolean {
        return code >= " "[0] && code <= "~"[0];
    }
    
    function InsertAt(char : String) : String {
        this.CatchInvalidCursorPos();
        return "`this.CommandString.SubStringFromRuneIndices(0, this.CursorIndex)``char``this.CommandString.SubStringFromRuneIndices(this.CursorIndex, this.CommandString.Count)`";
    }
    
    function RemoveAt(offset : Integer) : String {
        this.CatchInvalidCursorPos();
        if(this.CursorIndex - 1 + offset < 0 || this.CursorIndex + offset > this.CommandString.Count)
            offset = 0;
        return "`this.CommandString.SubStringFromRuneIndices(0, this.CursorIndex - 1 + offset)``this.CommandString.SubStringFromRuneIndices(this.CursorIndex + offset, this.CommandString.Count)`";
    }
    
    function CatchInvalidCursorPos() {
        if(this.CursorIndex > this.CommandString.Count)
            this.CursorIndex = this.CommandString.Count;
        else if(this.CursorIndex < 0)
            this.CursorIndex = 0;
    }
    
}
