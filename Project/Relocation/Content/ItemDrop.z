// AUTHOR: HUDSON GREEN
// CONTRIBUTORS: N/A

class ItemDrop : ZilchComponent {
    
    [Property] var ItemID : Integer = 0;
    [Property] var ItemAmount : Integer = 0;
    [Property] var ItemData : Integer = 0;
    [Property] var DespawnTime : Real = 5;
    
    var CurrLifetime : Real = 0.0;
    
    function Initialize(init : CogInitializer) {
        
        Zero.Connect(this.Owner, Events.CollisionStarted, this.OnCollisionStarted);
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        
    }
    
    function OnCollisionStarted(event : CollisionEvent) {
        
        if(event.OtherObject.Name == Relocation.EntityPlayer) {
            
            if(event.OtherObject.Inventory != null) {
                event.OtherObject.Inventory.GiveItemWithAmount(this.ItemID, false, this.ItemAmount);
                this.Owner.Destroy();
            }
            
        } else if(event.OtherObject.Name == this.Owner.Name) {
            
            if(this.CurrLifetime > event.OtherObject.ItemDrop.CurrLifetime && event.OtherObject.ItemDrop.ItemID == this.ItemID) {
                event.OtherObject.ItemDrop.ItemAmount += this.ItemAmount;
                this.Owner.Destroy();
            } else if(this.CurrLifetime >= event.OtherObject.ItemDrop.CurrLifetime) {
                var rand = Random();
                this.Owner.Transform.Translation += Real3(rand.Range(-0.25, 0.25), 0.0, 0.0);
            }
            
        }
        
    }
    
    function OnLogicUpdate(event : UpdateEvent) {
        
        if(this.CurrLifetime > this.DespawnTime * 60) {
            this.Owner.Destroy();
        } else {
            this.CurrLifetime += event.Dt;
        }
        
        if(this.ItemID != 0 && this.Owner.Sprite.SpriteSource == SpriteSource.MissingTexture) {
            this.Owner.Sprite.SpriteSource = Relocation.Items[this.ItemID].Sprite;
        }
        
    }
    
}
